buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.0'
  }
}

project("guide"){
  apply plugin: "groovy"
  apply plugin: 'org.asciidoctor.gradle.asciidoctor'
  asciidoctor {
    version = '1.6.0.SNAPSHOT'
    options = [
      attributes : [ 
        'source-highlighter' : 'coderay',
        toc : 'left',
        sourcedir : "${project.rootDir}/labs"
      ]
    ]
  }
}

ext.springVersion = "4.1.2.RELEASE"

subprojects {
  apply plugin:"java"
  apply plugin: 'project-report'
  sourceCompatibility = 1.6
  version = '1.0'

  repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/release/" }
    maven { url "http://repo.spring.io/milestone/" }
  }
  dependencies {
    testCompile "org.springframework:spring-test:$springVersion"
    testCompile 'junit:junit:4.11'
    compile 'org.slf4j:slf4j-log4j12:1.7.6'
  }
}

project(":labs:core"){
  dependencies {
    compile "org.hibernate:hibernate-entitymanager:4.3.1.Final"
    compile "org.apache.commons:commons-lang3:3.2.1"
  }
}

project(":labs:mailer"){
  dependencies {
    compile "org.springframework:spring-context-support:$springVersion"
    compile 'com.sun.mail:javax.mail:1.5.2'
    compile 'org.freemarker:freemarker:2.3.21'
  }
}

project(":labs:jms"){
  dependencies {
    compile project(":labs:repositoriesAndServices")
    compile 'org.apache.activemq:activemq-all:5.10.0'
    compile 'org.apache.activemq:activemq-spring:5.10.0'
    compile 'com.thoughtworks.xstream:xstream:1.4.7'
    compile "org.springframework:spring-jms:$springVersion"
  }
}

project(":labs:repositoriesAndServices") {
  dependencies {
    compile project(":labs:core")
    compile "org.springframework:spring-context:$springVersion"
    compile "org.springframework:spring-orm:$springVersion"
    compile "org.springframework:spring-aspects:$springVersion"

    def excludesList = ['slf4j-api','jcl-over-slf4j','spring-orm','spring-context','spring-aop','spring-tx','spring-beans','spring-core']
    compile("org.springframework.data:spring-data-jpa:1.4.3.RELEASE"){
      excludesList.each { m -> exclude module:m }
    }
    compile 'com.h2database:h2:1.3.175'
  }
}

project(":labs:web") {
  apply plugin: 'war'
  apply plugin:"jetty"

  //httpPort = 8081

  jettyRun {
    contextPath = 'trackbox'
    reload = 'automatic'
    scanIntervalSeconds = 10
  }
  dependencies {
    compile project(":labs:repositoriesAndServices")
    compile project(":labs:jms")
    compile "org.springframework:spring-webmvc:$springVersion"
    compile "org.apache.commons:commons-lang3:3.2.1"
    compile "javax.validation:validation-api:1.1.0.Final"
    compile("org.hibernate:hibernate-validator:5.0.3.Final"){
      exclude module:'jboss-logging'
    }
    providedCompile "javax.servlet:javax.servlet-api:3.1.0"
    runtime 'jstl:jstl:1.2'
    compile 'commons-fileupload:commons-fileupload:1.3.1'
    compile 'net.sourceforge.jexcelapi:jxl:2.6.12'
    compile 'com.lowagie:itext:4.2.1'
  }
}

// tag::jobs[]
project(":labs:jobs"){
  apply plugin: 'war'
  apply plugin:"jetty"
  httpPort = 8081
  jettyRun {
    contextPath = 'jobs'
  }
  dependencies {
    compile project(":labs:repositoriesAndServices")
    compile "org.springframework:spring-web:$springVersion"
    compile "org.springframework:spring-context-support:$springVersion"
    compile "org.springframework:spring-tx:$springVersion"
    compile 'org.quartz-scheduler:quartz:2.2.1'
  }
}
// end::jobs[]
